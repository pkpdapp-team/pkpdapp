[[model]]
name: 1-compartment QSS TMDD PK Model
author: Michael Gertz, Soledad Castano

# All parameter values are placeholders and can be adjusted in the app under Model/Parameters

# Initial values:
PKCompartment.Aa = 0
PKCompartment.A1_t = 0
PKCompartment.AT1_t = PKCompartment.CT1_0*PKCompartment.V1
PKCompartment.AUC1_f = 0
PKCompartment.AUC1_b = 0
PKCompartment.AUC1_t = 0
PKCompartment.AUCT1_f = 0
PKCompartment.AUCT1_b = 0
PKCompartment.AUCT1_t = 0
PKCompartment.AUCe = 0
PKCompartment.Ce = 0

[environment]
t = 0 in [h] bind time


[PKCompartment]

tlag = 0 in [h]
  desc: Absorption lag time from extravasc compartment

ka = 1 in [1/h]
  desc: First order absorption rate constant  

F = 1 in [dimensionless]
  desc: Fraction absorbed / bioavailability 
	
V1 = 1 in [L]
  desc: Volume of the central compartment	
	
CL = 1 in [L/h]
  desc: Linear clearance from central compartment

CLmax = 0 in [L/h]
  desc: Maximum nonlinear clearance from central compartment (CLmax = Vmax/Km)

Km = 1 in [pmol/L]
  desc: Michaelis-Menten constant 	

CT1_0 = 1 in [pmol/L]
  desc: Baseline concentration of the target in the central compartment

KSS = 1 in [pmol/L]
  desc: Apparent dissociation constant [KSS = (kint+koff)/kon], if kint << koff KSS ~= KD and if kint >> koff KSS > KD]
	
kdegT = 1 in [1/h]
  desc: Target degradation rate constant

kint = 1 in [1/h]
  desc: Complex internalization rate constant
	
ke0 = 1 in [1/h]
  desc: Equilibration rate constant effect compartment
	
Kp = 1 in [dimensionless]
  desc: Effect compartment to plasma partitioning coefficient


b_term = C1_t - KSS - CT1_t in [pmol/L]
  desc: b term of quadratic equation
	
c_term = C1_t * KSS in [pmol/L*pmol/L]
  desc: c term of quadratic equation

C1_t = A1_t/V1 in [pmol/L]
  desc: Concentration of TOTAL drug in the central compartment	

CT1_t = AT1_t/V1 in [pmol/L]	
  desc: Concentration of TOTAL target in the central compartment	
	
C1_f = 0.5 * (b_term + (b_term^2 + 4*c_term)^0.5) in [pmol/L]
  desc: Concentration of FREE drug in the central compartment

C1_b = C1_t - C1_f in [pmol/L]
  desc: Concentration of BOUND drug in the central compartment

CT1_b = C1_b in [pmol/L]
  desc: Concentration of BOUND target in the central compartment

CT1_f = CT1_t - CT1_b in [pmol/L]
  desc: Concentration of FREE target in the central compartment
	
CL_t = CL + CLmax*(1/(1+(C1_f/Km))) in [L/h]
  desc: Total clearance as a function of time

RO_Model = CT1_b/CT1_t*100 in [dimensionless]
  desc: Receptor (Target) occupancy from TMDD model
	
PerInh = (1-CT1_f/CT1_t)*100 in [dimensionless]
  desc: Percent inhbition relative to TOTAL target	
	
PerInh_Baseline = (1-CT1_f/CT1_0)*100 in [dimensionless]	
  desc: Percent inhibition relative to the initial steady-state concentrations of target	

dot(Aa) = - ka*Aa in [pmol]
  desc: Amount of drug in the 'depo' compartment 

dot(A1_t) = ka*Aa*F - CL*C1_f - CLmax*(1/(1+(C1_f/Km)))*C1_f - kint*C1_b*V1 in [pmol]
  desc: Amount of TOTAL drug in the central compartment 

dot(AT1_t) = kdegT*V1*(CT1_0 - CT1_f) - kint*CT1_b*V1 in [pmol]
  desc: Amount of TOTAL target in the central compartment

dot(Ce) = ke0*(C1_f*Kp - Ce) in [pmol/L]	
  desc: Concentration of FREE drug in the effect compartment 
	
dot(AUC1_f) = C1_f in [pmol/L*h]
  desc: AUC of FREE drug in the central compartment
	
dot(AUC1_b) = C1_b in [pmol/L*h]
  desc: AUC of BOUND drug in the central compartment

dot(AUC1_t) = C1_t in [pmol/L*h]
  desc: AUC of TOTAL drug in the central compartment	
	
dot(AUCT1_f) = CT1_f in [pmol/L*h]
  desc: AUC of FREE target in the central compartment
	
dot(AUCT1_b) = CT1_b in [pmol/L*h]
  desc: AUC of BOUND target in the central compartment

dot(AUCT1_t) = CT1_t in [pmol/L*h]
  desc: AUC of TOTAL target in the central compartment	
	
dot(AUCe) = Ce in [pmol/L*h]
  desc: AUC of FREE drug in the effect compartment