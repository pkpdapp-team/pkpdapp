[[model]]
name: 1-compartment PK Model + effect compartment
author: Michael Gertz

# Initial values:
PKCompartment.Aa = 0
PKCompartment.A1 = 0
PKCompartment.AUC1 = 0
PKCompartment.AUCe = 0
PKCompartment.Ce = 0

[environment]
t = 0 in [h] bind time


[PKCompartment]

tlag = 1 in [h]
	desc: absorption lag time from extravasc compartment

ka = 1 in [1/h]
    desc: first order absorption rate constant  

F = 1 in [dimensionless]
    desc: fraction absorbed 
	
V1 = 1 in [L/kg]
    desc: Volume of the central compartment	
	
CL = 1 in [L/h/kg]
    desc: Linear clearance from central compartment 

CLmax = 1 in [L/h/kg]
    desc: maximum nonlinear Michaelis-Menten clearance; Vmax = CLmax*Km

Km = 1 in [pmol/L]
    desc: Michaelis-Menten constant 	
	
ke0 = 1 in [1/h]
	desc: equilibration rate constant effect compartment
	
Kpu = 1 in [dimensionless]
	desc: unbound effect compartment to total plasma partitioning coefficient
	
C1= A1/V1 in [pmol/L]
    desc: Concentration of drug in the central compartment unit pmol/L
	
CL_t = CL+CLmax*(1/(1+(C1/Km))) in [L/h/kg]
	desc: total clearance as function of time 

dot(Aa) = -ka*Aa in [pmol/kg]
    desc: Amount of drug in 'depo' compartment

dot(A1) =  ka*Aa*F -CL*C1 -CLmax*(1/(1+(C1/Km)))*C1 in [pmol/kg]
    desc: Amount of drug in central compartment 

dot(Ce) = ke0*(C1*Kpu - Ce) in [pmol/L]	
	desc: Concentration of drug in effect compartment
	
dot(AUC1) = C1 in [pmol/L*h]
	desc: AUC of the central compartment
	
dot(AUCe) = Ce in [pmol/L*h]
	desc: AUC of the effect compartment