[[model]]
name: 3-compartment PK Model + effect compartment
author: Michael Gertz

# Initial values:
PKCompartment.Aa = 0
PKCompartment.A1 = 0
PKCompartment.A2 = 0
PKCompartment.A3 = 0
PKCompartment.AUC1 = 0
PKCompartment.AUCe = 0
PKCompartment.Ce = 0

[environment]
t = 0 bind time
    

[PKCompartment]

tlag = 1 in [h]
	desc: absorption lag time from extravasc compartment (implementation tbd)

ka = 1 in [1/h]
    desc: first order absorption rate constant  

F = 1 in [dimensionless]
    desc: fraction absorbed 
	
V1 = 1 in [L/kg]
    desc: Volume of the central compartment	
	
V2 = 1 in [L/kg]
    desc: Volume of the first peripheral compartment	

V3 = 1 in [L/kg]
    desc: Volume of the second peripheral compartment	
	
CL = 1 in [L/h/kg]
    desc: Linear clearance from central compartment 
	
Q1 = 1 in [L/h/kg]
    desc: Intercompartmental clearance between central compartment and first peripheral compartment 	
	
Q2 = 1 in [L/h/kg]
    desc: Intercompartmental clearance between central compartment and second peripheral compartment 	

CLmax = 1 in [L/h/kg]
    desc: maximum nonlinear Michaelis-Menten clearance; Vmax = CLmax*Km 

Km = 1 in [pmol/L]
    desc: Michaelis-Menten constant 	
	
ke0 = 1 in [1/h]
	desc: equilibration rate constant effect compartment
	
Kpu = 1 in [dimensionless]
	desc: effect compartment to plasma partitioning coefficient
	
C1= A1/V1 in [pmol/L]
    desc: Concentration of drug in the central compartment unit pmol/L
	
C2= A2/V2 in [pmol/L]
    desc: Concentration of drug in the 1st peripheral compartment unit pmol/L

C3= A3/V3 in [pmol/L]
    desc: Concentration of drug in the 2nd peripheral compartment unit pmol/L	
	
CL_t = CL+CLmax*(1/(1+(C1/Km))) in [L/h/kg]
	desc: total clearance as function of time unit [L/h] or [L/h/kg] 	

dot(Aa) = -ka*Aa in [pmol]
    desc: Amount of drug in 'depo' compartment unit in pmol 

dot(A1) =  ka*Aa*F -Q1*(C1 - C2) -Q2*(C1 - C3) -CL*C1 -CLmax*(1/(1+(C1/Km)))*C1 in [pmol]
    desc: Amount of drug in central compartment unit in pmol 
	
dot(A2) =  Q1*(C1 - C2) in [pmol]
    desc: Amount of drug in first peripheral compartment unit in pmol	
	
dot(A3) =  Q2*(C1 - C3) in [pmol]
    desc: Amount of drug in second peripheral compartment unit in pmol		

dot(Ce) = ke0*(C1*Kpu - Ce)	in [pmol/L]
	desc: Concentration of drug in effect compartment unit in pmol/L 
	
dot(AUC1)= C1 in [pmol/L*h]
	desc: AUC of the central compartment unit pmol/L*h
	
dot(AUCe)= Ce in [pmol/L*h]
	desc: AUC of the effect compartment unit pmol/L*h	
