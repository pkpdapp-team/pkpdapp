[[model]]
name: 3-compartment PK Model + effect compartment
author: Michael Gertz

# Initial values:
PKCompartment.Aa = 0
PKCompartment.A1 = 0
PKCompartment.A2 = 0
PKCompartment.A3 = 0
PKCompartment.AUC1 = 0
PKCompartment.AUCe = 0
PKCompartment.Ce = 0

[environment]
t = 0 in [h] bind time


[PKCompartment]

tlag = 0 in [h]
	desc: Absorption lag time from extravasc compartment

ka = 1 in [1/h]
    desc: First order absorption rate constant  

F = 1 in [dimensionless]
    desc: Fraction absorbed/ bioavailability 
	
V1 = 1 in [L/kg]
    desc: Volume of the central compartment	
	
V2 = 1 in [L/kg]
    desc: Volume of the first peripheral compartment	

V3 = 1 in [L/kg]
    desc: Volume of the second peripheral compartment	
	
CL = 1 in [L/h/kg]
    desc: Linear clearance from central compartment 
	
Q1 = 1 in [L/h/kg]
    desc: Intercompartmental clearance between central compartment and first peripheral compartment 	
	
Q2 = 1 in [L/h/kg]
    desc: Intercompartmental clearance between central compartment and second peripheral compartment 	

CLmax = 0 in [L/h/kg]
    desc: Maximum nonlinear Michaelis-Menten clearance; CLmax = Vmax/Km

Km = 1 in [pmol/L]
    desc: Michaelis-Menten constant 	
	
ke0 = 1 in [1/h]
	desc: Equilibration rate constant effect compartment
	
Kpu = 1 in [dimensionless]
	desc: Effect compartment to plasma partitioning coefficient
	
C1= A1/V1 in [pmol/L]
    desc: Concentration of drug in the central compartment
	
C2= A2/V2 in [pmol/L]
    desc: Concentration of drug in the 1st peripheral compartment

C3= A3/V3 in [pmol/L]
    desc: Concentration of drug in the 2nd peripheral compartment	
	
CL_t = CL+CLmax*(1/(1+(C1/Km))) in [L/h/kg]
	desc: Total clearance as a function of time	

dot(Aa) = -ka*Aa in [pmol/kg]
    desc: Amount of drug in 'depo' compartment

dot(A1) =  ka*Aa*F -Q1*(C1 - C2) -Q2*(C1 - C3) -CL*C1 -CLmax*(1/(1+(C1/Km)))*C1 in [pmol/kg]
    desc: Amount of drug in central compartment
	
dot(A2) =  Q1*(C1 - C2) in [pmol/kg]
    desc: Amount of drug in first peripheral compartment	
	
dot(A3) =  Q2*(C1 - C3) in [pmol/kg]
    desc: Amount of drug in second peripheral compartment		

dot(Ce) = ke0*(C1*Kpu - Ce) in [pmol/L]	
	desc: Concentration of drug in effect compartment
	
dot(AUC1)= C1 in [pmol/L*h]
	desc: AUC of drug in the central compartment
	
dot(AUCe)= Ce in [pmol/L*h]
	desc: AUC of drug in the effect compartment