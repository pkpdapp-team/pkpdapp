[[model]]
name: Indirect effects model with precursor (for rebound/ tolerance) - inhibition of precursor elimination
author: Michael Gertz

# Initial values:
PDCompartment.P = PDCompartment.E0*PDCompartment.kdeg/(PDCompartment.kdegP*PDCompartment.FE)
PDCompartment.E = PDCompartment.E0


[environment]
t = 0 in [h] bind time


[PDCompartment]

kdegP = 0.1 in [1/h]
	desc: degradation rate constant of precursor
	
FE = 1 in [dimensionless]
	desc: fraction of precursor that is converted to effect
	
kdeg = 2 in [1/h]
	desc: degradation rate constant of the effect

E0 = 100 in [dimensionless]
	desc: baseline effect
	
Imax = 0.80 in [dimensionless]
	desc: maximal fold change from baseline (value range: 0 (no effect) to 1 (complete inhibition))
	
C50 = 100000 in [pmol/L]
	desc: concentration that produces half-maximal effects

HC = 1 in [dimensionless]
	desc: Hill coefficient
	
C_Drug = 1 in [pmol/L]
	desc: drug concentration driving the PD effect

P0 = E0*kdeg/(kdegP*FE) in [dimensionless]
	desc: baseline of precursor	

INH = C_Drug^HC/(C_Drug^HC+C50^HC) in [dimensionless]
	desc: Inhibition

dot(P) = kdegP*P0 - kdegP*P*(FE*(1-Imax*INH) + (1-FE)) in [dimensionless]
	desc: Precursor

dot(E) = kdegP*P*(FE*(1-Imax*INH)) -kdeg*E in [dimensionless]
	desc: PD effect