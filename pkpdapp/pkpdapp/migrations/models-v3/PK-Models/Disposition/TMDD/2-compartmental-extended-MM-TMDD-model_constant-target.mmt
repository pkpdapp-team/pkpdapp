[[model]]
name: 2-compartmental extended Michaelis-Menten TMDD model - constant target
author: Michael Gertz


# Initial values:
PKCompartment.A1_f = 0
PKCompartment.A2_f = 0
PKCompartment.AT1_t = 0

[environment]
t = 0 in [h] bind time


[PKCompartment]

F = 1 in [dimensionless]
  desc: Fraction absorbed / bioavailability 
  
V1 = 1 in [L]
  desc: Volume of the central compartment	
  
V2 = 1 in [L]
  desc: Volume of the peripheral compartment  
	
CL = 1 in [L/h]
  desc: Linear clearance from central compartment

Q1 = 1 in [L/h]
  desc: Intercompartmental clearance between central and peripheral compartment  

CT1_0 = 1 in [pmol/L]
	desc: Baseline concentration of the target in the central compartment

KSS = 1 in [pmol/L]
	desc: Apparent dissociation (Michaelis-Menten) constant [KSS = (kint1+koff)/kon], if kint1 << koff KSS ~= KD and if kint1 >> koff KSS > KD]
	
kdegT1 = 1 in [1/h]
	desc: Target degradation rate constant
	
kint1 = kdegT1 in [1/h]
	desc: Complex internalization rate constant
  
CLada = 0 in [L/h]
  desc: ADA-mediated clearance 

tada = 240 in [h]
  desc: Time at which ADA-mediated clearance occurs

CLimm = if(environment.t < tada , 
	0 , 
	CLada) in [L/h]
	

C1_f = A1_f/V1 in [pmol/L]
  desc: Concentration of FREE drug in the central compartment

C2_f = A2_f/V2 in [pmol/L]
  desc: Concentration of FREE drug in the peripheral compartment   

CT1_t = AT1_t/V1 in [pmol/L]	
  desc: Concentration of TOTAL target in the central compartment	
	
RO_Model = C1_f/(C1_f + KSS)*100 in [dimensionless]
  desc: Receptor (Target) occupancy
	
PerInh = 100-RO_Model in [dimensionless]
  desc: Percent inhibition relative to total target
	
PerInh_Baseline = CT1_t*RO_Model/CT1_0*100 in [dimensionless]	
  desc: Percent inhibition relative to the initial steady-state level of target
  
RateAbs = 0 in [pmol/h] 
  desc: Absorption rate   
  

dot(A1_f) = RateAbs*F - CL*C1_f - Q1*(C1_f - C2_f) - (kint1*V1*CT1_t)*C1_f/(C1_f + KSS) - CLimm*C1_f in [pmol]
  desc: Amount of TOTAL drug in the central compartment 
  
dot(A2_f) = Q1*(C1_f - C2_f) - CLimm*C2_f*V2/V1 in [pmol]
  desc: Amount of drug in the peripheral compartment    

dot(AT1_t) = kdegT1*V1*(CT1_0 - CT1_t) - (CLimm/V1+kint1-kdegT1)*V1*CT1_t*C1_f/(C1_f + KSS) in [pmol]
  desc: Amount of TOTAL target in the central compartment